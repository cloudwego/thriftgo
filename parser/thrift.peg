package parser


type ThriftIDL Peg {
}


Document <- Header* Definition* Skip !.

Header <- Skip (Include / CppInclude / HsInclude / Namespace / Package) SkipLine

Include <- INCLUDE Literal

CppInclude <- CPPINCLUDE Literal

HsInclude <- HSINCLUDE Literal

Namespace <- NAMESPACE NamespaceScope (Identifier / Literal) Annotations?

Package <- ReservedComments StructuredAnnotations? PACKAGE Literal? ListSeparator?

NamespaceScope <- Skip
    <'*'> Indent*
    / Identifier

Definition <- ReservedComments Skip (Const / Typedef / Enum / Service / Interaction / Struct / Union / Exception) Annotations? SkipLine

Const <- StructuredAnnotations? CONST FieldType Identifier EQUAL ConstValue ListSeparator?

Typedef <- StructuredAnnotations? TYPEDEF FieldType Identifier ListSeparator?

Enum  <- StructuredAnnotations? ENUM Identifier LWING (ReservedComments StructuredAnnotations? Identifier (EQUAL IntConstant)? Annotations? ListSeparator? ReservedEndLineComments SkipLine)* RWING

Service <- StructuredAnnotations? SERVICE Identifier ( EXTENDS Identifier )? LWING (Function / Performs)* RWING

Interaction <- StructuredAnnotations? INTERACTION Identifier LWING Function* RWING

Struct <- StructuredAnnotations? STRUCT Identifier LWING Field* RWING

Union <- StructuredAnnotations? UNION Identifier LWING Field* RWING

Exception <- StructuredAnnotations? ExceptionMeta? EXCEPTION Identifier LWING Field* RWING

ExceptionMeta <- ExceptionLabel*

ExceptionLabel <- SAFE / UNSAFE / TRANSIENT / PERMANENT / STATEFUL / SERVER / CLIENT

Field <- ReservedComments Skip StructuredAnnotations? FieldId? FieldReq? FieldType Identifier (EQUAL ConstValue)? Annotations? ListSeparator? ReservedEndLineComments SkipLine

FieldId <- Skip IntConstant COLON Indent*

FieldReq <- Skip <('required' / 'optional')> Indent*

Function  <- ReservedComments Skip StructuredAnnotations? FunctionMeta? ReturnClause Identifier LPAR Field* RPAR Throws? Annotations? ListSeparator? SkipLine

ReturnClause <- FunctionType (COMMA FunctionType)? (COMMA FunctionType)?

FunctionType  <- VOID / Sink / Stream / FieldType

FunctionMeta <- FunctionLabel?

FunctionLabel <- ONEWAY / READONLY / IDEMPOTENT

Performs <- ReservedComments Skip PERFORMS Identifier ListSeparator? SkipLine

Throws <- THROWS LPAR Field* RPAR

FieldType  <- (ContainerType / BaseType / Identifier) Annotations?

BaseType <- (BOOL / BYTE / I8 / I16 / I32 / I64 / DOUBLE / STRING / BINARY)

ContainerType <- MapType / SetType / ListType

MapType <-  MAP CppType? LPOINT FieldType COMMA FieldType RPOINT

SetType <- SET CppType? LPOINT FieldType RPOINT

ListType <- LIST LPOINT FieldType RPOINT CppType?

CppType <- CPPTYPE Literal

Sink <- SINK LPOINT FieldType Throws? COMMA FieldType Throws? RPOINT

Stream <- STREAM LPOINT FieldType Throws? RPOINT

ConstValue <- DoubleConstant / IntConstant / Literal / ConstStruct / Identifier / ConstList / ConstMap

IntConstant <- Skip < [+\-]? '0' [bB] [01]+ / [+\-]? '0' [xX] ([0-9] / [A-Z] / [a-z])+ / '0o' Digit+ / [+\-]? Digit+ > Indent*

DoubleConstant  <- Skip <[+\-]? (
        Digit* '.' Digit+  Exponent?
    /   Digit+ Exponent
    )> Indent*

Exponent <- ('e' / 'E') IntConstant

Annotations <- LPAR Annotation* RPAR

Annotation <- ((Identifier EQUAL ConstValue) / Identifier) ListSeparator?

StructuredAnnotations <- StructuredAnnotation*

StructuredAnnotation <- AT (ConstStruct / Identifier)

ConstList  <- LBRK (ConstValue ListSeparator?)* RBRK

ConstMap  <- LWING (ConstValue COLON ConstValue ListSeparator?)* RWING

ConstStruct <- Identifier LWING (Identifier EQUAL ConstValue ListSeparator?)* RWING

EscapeLiteralChar <- '\\' ["'] / '\\\\'

Literal <- Skip '"' <(EscapeLiteralChar / !'"' .)*> '"' Indent*
        / Skip "'" <(EscapeLiteralChar / !"'" .)*> "'" Indent*

Identifier <- Skip <Letter ( Letter / Digit / '.' )*> Indent*

ListSeparator <- Skip (',' / ';') Indent*

Letter <- [A-Z] / [a-z] / '_'
LetterOrDigit <- [a-z] / [A-Z] / [0-9] / [_$]

Digit <- [0-9]

ReservedComments <- Skip
ReservedEndLineComments <- SkipLine

Skip <- (Space / Comment)*
SkipLine <- (Indent / Comment)*

Space <- (Indent / CarriageReturnLineFeed)+
Indent <- [ \t\v]
CarriageReturnLineFeed <- [\r\n]

Comment <- LongComment / LineComment / UnixComment
LongComment <- '/*' (!'*/' .)* '*/'
LineComment <- '//' (![\r\n] .)*
UnixComment <- '#' (![\r\n] .)*

BOOL        <- Skip <'bool'>        !LetterOrDigit  Indent*
BYTE        <- Skip <'byte'>        !LetterOrDigit  Indent*
I8          <- Skip <'i8'>          !LetterOrDigit  Indent*
I16         <- Skip <'i16'>         !LetterOrDigit  Indent*
I32         <- Skip <'i32'>         !LetterOrDigit  Indent*
I64         <- Skip <'i64'>         !LetterOrDigit  Indent*
DOUBLE      <- Skip <'double'>      !LetterOrDigit  Indent*
STRING      <- Skip <'string'>      !LetterOrDigit  Indent*
BINARY      <- Skip <'binary'>      !LetterOrDigit  Indent*
CONST       <- Skip 'const'         !LetterOrDigit  Indent*
ONEWAY      <- Skip 'oneway'        !LetterOrDigit  Indent*
READONLY    <- Skip 'readonly'      !LetterOrDigit  Indent*
IDEMPOTENT  <- Skip 'idempotent'    !LetterOrDigit  Indent*
TYPEDEF     <- Skip 'typedef'       !LetterOrDigit  Indent*
MAP         <- Skip 'map'           !LetterOrDigit  Indent*
SET         <- Skip 'set'           !LetterOrDigit  Indent*
LIST        <- Skip 'list'          !LetterOrDigit  Indent*
VOID        <- Skip 'void'          !LetterOrDigit  Indent*
THROWS      <- Skip 'throws'        !LetterOrDigit  Indent*
EXCEPTION   <- Skip 'exception'     !LetterOrDigit  Indent*
SAFE        <- Skip 'safe'          !LetterOrDigit  Indent*
UNSAFE      <- Skip 'unsafe'        !LetterOrDigit  Indent*
TRANSIENT   <- Skip 'transient'     !LetterOrDigit  Indent*
PERMANENT   <- Skip 'permanent'     !LetterOrDigit  Indent*
STATEFUL    <- Skip 'stateful'      !LetterOrDigit  Indent*
SERVER      <- Skip 'server'        !LetterOrDigit  Indent*
CLIENT      <- Skip 'client'        !LetterOrDigit  Indent*
EXTENDS     <- Skip 'extends'       !LetterOrDigit  Indent*
SERVICE     <- Skip 'service'       !LetterOrDigit  Indent*
INTERACTION <- Skip 'interaction'   !LetterOrDigit  Indent*
STRUCT      <- Skip 'struct'        !LetterOrDigit  Indent*
UNION       <- Skip 'union'         !LetterOrDigit  Indent*
ENUM        <- Skip 'enum'          !LetterOrDigit  Indent*
INCLUDE     <- Skip 'include'       !LetterOrDigit  Indent*
CPPINCLUDE  <- Skip 'cpp_include'   !LetterOrDigit  Indent*
HSINCLUDE   <- Skip 'hs_include'    !LetterOrDigit  Indent*
NAMESPACE   <- Skip 'namespace'     !LetterOrDigit  Indent*
PACKAGE     <- Skip 'package'       !LetterOrDigit  Indent*
CPPTYPE     <- Skip 'cpp_type'      !LetterOrDigit  Indent*
PERFORMS    <- Skip 'performs'      !LetterOrDigit  Indent*
SINK        <- Skip 'sink'          !LetterOrDigit  Indent*
STREAM      <- Skip 'stream'        !LetterOrDigit  Indent*
LBRK        <- Skip '['     Indent*
RBRK        <- Skip ']'     Indent*
LWING       <- Skip '{'     Indent*
RWING       <- Skip '}'     Indent*
EQUAL       <- Skip '='     Indent*
LPOINT      <- Skip '<'     Indent*
RPOINT      <- Skip '>'     Indent*
COMMA       <- Skip ','     Indent*
LPAR        <- Skip '('     Indent*
RPAR        <- Skip ')'     Indent*
COLON       <- Skip ':'     Indent*
AT          <- Skip '@'     Indent*
